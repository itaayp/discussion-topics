defmodule Discuss.CommentsChannel do
  @moduledoc """
  This is the Comment's Web Socket Channel
  """

  use Discuss.Web, :channel
  alias Discuss.{Topic, Comment}

  @doc """
  Mandatory function for channels.

  This function will create a new connection to the Web Socket
  """
  def join("comments:" <> topic_id, _params, socket) do
    topic_id = String.to_integer(topic_id)
    topic = Topic
      |> Repo.get(topic_id)
      |> Repo.preload(comments: [:user])

    {:ok, %{comments: topic.comments}, assign(socket, :topic, topic)}
  end

  @doc """
  Mandatory function for channels.

  This function begins the insertion of a new comment in the database
  """
  def handle_in(name, %{"content" => content}, socket) do
    topic = socket.assigns.topic
    user_id = socket.assigns.user_id

    changeset = topic
    |> build_assoc(:comments, user_id: user_id)
    |> Comment.changeset(%{content: content})

    case Repo.insert(changeset) do
      {:ok, comment} ->
        broadcast!(socket, "comments:#{socket.assigns.topic.id}:new",
          %{comment: comment}
        )
        {:reply, :ok, socket}
      {:error, _reason} ->
        {:reply, {:error, %{errors: changeset}}, socket}
    end
  end
end
